apply plugin: "java"
apply plugin: "maven"

group = "org.sotnya"
description = """Ukrainian lemmatizer plugin for ElasticSearch"""
version = "1.5.2"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

String propertiesFile = "./plugin-descriptor.properties"
String esVersion = "2.4.6"
String luceneVersion = "5.5.4"

repositories {
    mavenCentral()

    maven { url "https://oss.sonatype.org/content/repositories/releases" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

dependencies {
    compile group: "org.elasticsearch", name: "elasticsearch", version: esVersion
    compile group: "org.apache.lucene", name: "lucene-analyzers-morfologik", version: luceneVersion
    compile group: "ua.net.nlp", name: "morfologik-ukrainian-search", version: "3.9.0"

    testCompile group: "junit", name: "junit", version: "4.12"
    testCompile group: "net.java.dev.jna", name: "jna", version: "4.5.0"
    testCompile group: "org.apache.lucene", name: "lucene-test-framework", version: luceneVersion
    testCompile group: "org.elasticsearch", name: "elasticsearch", version: esVersion, classifier: "tests"
    testCompile group: "org.hamcrest", name: "hamcrest-library", version: "1.3"
}

File explodedDistDir = new File(distsDir, "exploded")

jar.manifest.mainAttributes(
        "Implementation-Title": "ElasticSearch::Plugins::Analysis::UkrainianLemmatizer",
        "Implementation-Version": rootProject.version,
        "Implementation-Date": new Date().format("yyyy-MM-dd HH:mm:ss"))

task explodedDist(dependsOn: [jar], description: "Builds the plugin zip file") {
    doLast {
        [explodedDistDir]*.mkdirs()

        copy {
            from configurations.compile
            into explodedDistDir

            include "elasticsearch-ukrainian-lemmatizer*.jar"
            include "lucene-analyzers-morfologik*.jar"
            include "morfologik-fsa*.jar"
            include "morfologik-stemming*.jar"
            include "morfologik-ukrainian-search*.jar"
        }

        copy {
            from libsDir
            into explodedDistDir
        }

        copy {
            from propertiesFile
            into explodedDistDir
            expand version: version, es_version: esVersion
        }
    }
}

task zip(type: Zip, dependsOn: [explodedDist]) {
    from(explodedDistDir) {
    }
}

task release(dependsOn: [zip]) {
    doLast {
        ant.delete(dir: explodedDistDir)
    }
}

test {
    systemProperty 'tests.security.manager', 'false'
    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true

    // listen to events in the test execution lifecycle
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }
}
