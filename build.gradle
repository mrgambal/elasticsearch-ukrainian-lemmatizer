apply plugin: "java"
apply plugin: "maven-publish"

group = "org.sotnya"
description = """Ukrainian lemmatizer plugin for ElasticSearch"""
version = "1.6.0"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

String propertiesFile = "./plugin-descriptor.properties"
String esVersion = "5.6.16"
String luceneVersion = "6.6.1"

repositories {
    mavenCentral()

    maven { url "https://oss.sonatype.org/content/repositories/releases" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

dependencies {
    implementation group: "org.apache.logging.log4j", name: "log4j-api", version: "2.11.1"
    implementation group: "org.apache.logging.log4j", name: "log4j-core", version: "2.11.1"
    implementation group: "org.elasticsearch", name: "elasticsearch", version: esVersion
    implementation group: "org.apache.lucene", name: "lucene-analyzers-morfologik", version: luceneVersion
    implementation group: "ua.net.nlp", name: "morfologik-ukrainian-search", version: "4.9.1"

    testImplementation group: "org.apache.lucene", name: "lucene-test-framework", version: luceneVersion
    testImplementation group: "org.elasticsearch.test", name: "framework", version: esVersion
    testImplementation group: "org.hamcrest", name: "hamcrest-all", version: "1.3"
}

Directory explodedDistDir = distsDirectory.dir("exploded").get()
Directory esDir = explodedDistDir.dir("elasticsearch")

jar.manifest.mainAttributes(
        "Implementation-Title": "ElasticSearch::Plugins::Analysis::UkrainianLemmatizer",
        "Implementation-Version": rootProject.version,
        "Implementation-Date": new Date().format("yyyy-MM-dd HH:mm:ss"))

task explodedDist(dependsOn: [jar], description: "Builds the plugin zip file") {
    doLast {
        copy {
            from configurations.compileClasspath
            into esDir

            include "elasticsearch-ukrainian-lemmatizer*.jar"
            include "lucene-analyzers-morfologik*.jar"
            include "morfologik-fsa*.jar"
            include "morfologik-stemming*.jar"
            include "morfologik-ukrainian-search*.jar"
        }

        copy {
            from libsDirectory
            into esDir
        }

        copy {
            from propertiesFile
            into esDir
            expand version: version, es_version: esVersion
        }
    }
}

task zip(type: Zip, dependsOn: [explodedDist]) {
    from(explodedDistDir) {}
}

task release(dependsOn: [zip]) {
    doLast {
        ant.delete(dir: explodedDistDir)
    }
}

test {
    systemProperty 'tests.security.manager', 'false'
    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true

    // listen to events in the test execution lifecycle
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }
}
